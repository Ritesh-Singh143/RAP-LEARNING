@Metadata.layer: #PARTNER
@Search.searchable: true
@UI:{ headerInfo:{ typeName: 'Travel',
                   typeNamePlural: 'Travels',
                   title:{ type: #STANDARD, value: 'Agency_Name' },
                   description:{ type: #STANDARD, value: 'TravelId' } },
                   presentationVariant: [{ sortOrder: [{ by: 'TravelId',direction: #ASC }] }] }
annotate entity ZC_TRAVEL_RS1 with
{
  @UI.facet: [ {  id : 'TravelStatus',
                  type: #DATAPOINT_REFERENCE,
                  purpose: #HEADER,
                  position: 10,
                  targetQualifier: 'TravelStatus'
                },
               {  id : 'TraveltotalPrice',
                  type: #DATAPOINT_REFERENCE,
                  purpose: #HEADER,
                  position: 20,
                  targetQualifier: 'TotalPrice'
                },
                {  id : 'TravelBookingPrice',
                  type: #DATAPOINT_REFERENCE,
                  purpose: #HEADER,
                  position: 30,
                  targetQualifier: 'TotalBookPrice'
                },
                { label: 'General Info',
                  id : 'GeneralInfo',
                  type: #COLLECTION,
                  position: 10 },
               {  id : 'Travel',
                  parentId: 'GeneralInfo',
                  type: #IDENTIFICATION_REFERENCE,
                  purpose: #STANDARD,
                  label : 'Travel',
                  position: 10
               },
               { id : 'Prices',
                  parentId: 'GeneralInfo',
                  type: #FIELDGROUP_REFERENCE,
                  purpose: #STANDARD,
                  label : 'Prices',
                  position: 20,
                  targetQualifier: 'Prices' },
               { id : 'Dates',
                  parentId: 'GeneralInfo',
                  type: #FIELDGROUP_REFERENCE,
                  purpose: #STANDARD,
                  label : 'Dates',
                  position: 30,
                  targetQualifier: 'Dates' },
                 { id : 'Others',
                  parentId: 'GeneralInfo',
                  type: #FIELDGROUP_REFERENCE,
                  purpose: #STANDARD,
                  label : 'Others',
                  position: 40,
                  targetQualifier: 'Others' },
               {  id : 'Booking',
                  type: #LINEITEM_REFERENCE,
                  purpose: #STANDARD,
                  label : 'Booking',
                  position: 20,
                  targetElement: '_booking'
                } ]
  @Search.defaultSearchElement: true
  @UI:{ lineItem: [{ position: 10, label: 'Travel ID' },{ dataAction: 'copyRecord', type: #FOR_ACTION, label: 'Copy Record' }],
                        selectionField: [{ position: 10 }], identification: [{ position: 10, label: 'Travel ID' },{ dataAction: 'copyRecord', type: #FOR_ACTION, label: 'Copy Record' }] }

  TravelId;
  @Search.defaultSearchElement: true
  @UI:{ lineItem: [{ position: 20, label: 'Agency ID' },{ dataAction: 'createInstance', type: #FOR_ACTION, label: 'Create Instance' }],
        selectionField: [{ position: 20 }],
        identification: [{ position: 20, label: 'Agency ID' }] }
  AgencyId;
  @Search.defaultSearchElement: true
  @Search.fuzzinessThreshold: 0.7
  @UI:{ lineItem: [{ position: 21, label: 'Agency Name' },{ dataAction: 'rejectTravel', type: #FOR_ACTION, label: 'Reject Travel' }],
        identification: [{ position: 30, label: 'Agency Name' },{ dataAction: 'rejectTravel', type: #FOR_ACTION, label: 'Reject Travel' }] }
  Agency_Name;
  @Search.defaultSearchElement: true
  @UI:{ lineItem: [{ position: 30, label: 'Customer ID' }],selectionField: [{ position: 30 }],identification: [{ position: 40, label: 'Customer ID' }] }
  @Consumption.valueHelpDefinition: [{ entity :{ name: '/DMO/I_CUSTOMER',element: 'CustomerID' } }]
  CustomerId;
  @UI:{ lineItem: [{ position: 31, label: 'Customer Name' }],identification: [{ position: 50, label: 'Customer Name' }] }
  Customer_Name;
  @UI:{ lineItem: [{ position: 40, label: 'Begin Date' }],
        selectionField: [{ position: 40 }] }
  @UI.fieldGroup: [{ qualifier: 'Dates',label: 'Start Date ',position: 10 }]
  @Consumption:{ filter:{ selectionType: #INTERVAL, multipleSelections: false } }
  BeginDate;
  @UI:{ lineItem: [{ position: 50, label: 'End Date' }] }
  @UI.fieldGroup: [{ qualifier: 'Dates',label: 'End Date ',position: 20 }]
  EndDate;
  @UI:{ lineItem: [{ position: 60, label: 'Booking Fare' } ] }
  @UI.dataPoint:{visualization: #RATING, qualifier: 'TotalBookPrice', title: 'Travelling Experience' }//targetValue: 5,
  @UI.fieldGroup: [{ qualifier: 'Prices',label: 'Booking Fee ',position: 10 }]
  BookingFee;
  @UI:{ lineItem: [{ type: #AS_DATAPOINT,position: 70, label: 'Total Price' }] }
  @UI.dataPoint:{visualization: #PROGRESS,targetValue: 50000, qualifier: 'TotalPrice',title: 'Total Price' }
  @UI.fieldGroup: [{ type: #AS_DATAPOINT,qualifier: 'Prices',label: 'Total Price ',position: 20 }]
  TotalPrice;
  @Search.defaultSearchElement: true
  @UI:{ lineItem: [{ position: 80, label: 'Currency Code' }] }
  @Consumption.valueHelpDefinition: [{ entity :{ name: 'I_CURRENCY',element: 'Currency' } }]
  @UI.fieldGroup: [{ qualifier: 'Prices',label: 'Currency Code',position: 30 }]
  CurrencyCode;
  @UI:{ lineItem: [{ position: 90, label: 'Description' }] }
  @UI.fieldGroup: [{ qualifier: 'Others',label: 'Description',position: 10 }]
  Description;
  @UI:{ lineItem: [{ position: 100, label: 'Status', criticality: 'OverallCriticality'},
                   { dataAction: 'AcceptTravel', type: #FOR_ACTION, label: 'Accept Travel' } ],
        selectionField: [{ position: 100 }] }
  @UI.textArrangement: #TEXT_ONLY
  @Consumption:{ valueHelpDefinition: [{ entity :{ name: '/DMO/I_Overall_Status_VH',element: 'OverallStatus' } }],
                 filter :{ mandatory: true } }
  @UI.dataPoint:{ qualifier: 'TravelStatus',title: 'Status',criticality: 'OverallCriticality' }
  @UI.fieldGroup: [{ qualifier: 'Others',label: 'Status',position: 20,criticality: 'OverallCriticality' },{ dataAction: 'AcceptTravel', type: #FOR_ACTION, label: 'Accept Travel' }]
  OverallStatus;
  @UI:{ lineItem: [{ position: 110, label: 'Created By' }] }
  @UI.fieldGroup: [{ qualifier: 'Others',label: 'Created By',position: 30 }]
  CreatedBy;
  @UI:{ lineItem: [{ position: 120, label: 'Created At' }] }
  @UI.fieldGroup: [{ qualifier: 'Others',label: 'Created At',position: 40 }]
  CreatedAt;
  @UI.hidden: true
  @UI:{ lineItem: [{ position: 130, label: 'Last Changed By' }] }
  @UI.fieldGroup: [{ qualifier: 'Others',label: 'Last Changed By',position: 50 }]
  LastChangedBy;
  @UI.hidden: true
  @UI:{ lineItem: [{ position: 140, label: 'Last Changed At' }] }
  @UI.fieldGroup: [{ qualifier: 'Others',label: 'Last Changed At',position: 60 }]
  LastChangedAt;

}
