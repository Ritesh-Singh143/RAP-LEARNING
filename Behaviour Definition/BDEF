managed implementation in class zbp_i_travel_rs1 unique;
strict ( 2 );
with draft;

define behavior for ZI_TRAVEL_RS1 alias Travel
implementation in class zbp_i_travel_rs1 unique
draft table zdraft_travel_rs
persistent table zrs_travel_01
with additional save
lock master
total etag LastChangedAt
authorization master ( instance )
etag master LastChangedAt
{
  create;
  update;
  delete;

  field ( readonly : update ) TravelId;
  field ( readonly ) CreatedBy, LastChangedAt, LastChangedBy;
  //field ( mandatory : create ) AgencyId, CustomerId;

  action ( features : instance ) rejectTravel result [1] $self;
  action ( features : instance ) AcceptTravel result [1] $self;

  factory action copyRecord [1];
  static factory action createInstance [1];

  determination calculateTotalPrice on modify { field BookingFee; }

  side effects
  {
    field BookingFee affects field TotalPrice;
  }

  association _booking { create ( features : instance ); with draft; }
  //association _booksuppl {  with draft; }

  draft action Edit;
  draft action Activate optimized;
  draft action Discard;
  draft action Resume;
  draft determine action Prepare;


  mapping for zrs_travel_01
    {
      TravelId      = travel_id;
      AgencyId      = agency_id;
      CustomerId    = customer_id;
      BeginDate     = begin_date;
      EndDate       = end_date;
      BookingFee    = booking_fee;
      TotalPrice    = total_price;
      CurrencyCode  = currency_code;
      Description   = description;
      OverallStatus = overall_status;
      CreatedBy     = created_by;
      CreatedAt     = created_at;
      LastChangedAt = last_changed_at;
      LastChangedBy = last_changed_by;
    }

}

define behavior for ZI_BOOKING_RS1 alias Booking
implementation in class zbp_i_travel_rs1 unique
persistent table zrs_booking_01
draft table zdraft_bookng_rs
lock dependent by _travel
authorization dependent by _travel
etag master LastChangedAt
{
  update;
  delete;
  field ( readonly ) TravelId, LastChangedAt;
  field ( readonly : update ) BookingId;

  field ( mandatory ) CarrierId, ConnectionId, FlightDate, BookingStatus;

  determination populatebookingDate on save { field TravelId, BookingId, CustomerId; create; }
  determination calculateBookingFare on modify { field FlightPrice; create; update; delete; }

  association _travel { with draft; }
  association _booksuppl { create ( features : instance ); with draft; }

  side effects
  {
    field FlightPrice affects field _travel.BookingFee, field _travel.TotalPrice;
  }

  mapping for zrs_booking_01
    {
      TravelId      = travel_id;
      BookingId     = booking_id;
      BookingDate   = booking_date;
      CustomerId    = customer_id;
      CarrierId     = carrier_id;
      ConnectionId  = connection_id;
      FlightDate    = flight_date;
      FlightPrice   = flight_price;
      CurrencyCode  = currency_code;
      BookingStatus = booking_status;
      LastChangedAt = last_changed_at;
    }
}

define behavior for ZI_BOOKSUPPL_RS1 alias BookingSuppl
implementation in class zbp_i_travel_rs1 unique
//persistent table zrs_booksuppl_01
with unmanaged save
draft table zdraft_bokngs_rs
lock dependent by _travel
authorization dependent by _travel
etag master LastChangedAt
{
  update;
  delete;

  field ( readonly ) TravelId, BookingId, LastChangedAt;
  //field ( mandatory )  SupplementId,price;

  association _travel { with draft; }
  association _booking { with draft; }

  validation validateSupplementId on save { create; }
  determination calculateBookingFare on modify { field Price; create; update; delete; }

  side effects
  {
    field Price affects field _travel.BookingFee, field _travel.TotalPrice;
  }

  mapping for zrs_booksuppl_01
    {
      TravelId            = travel_id;
      BookingId           = booking_id;
      BookingSupplementId = booking_supplement_id;
      SupplementId        = supplement_id;
      Price               = price;
      CurrencyCode        = currency_code;
      LastChangedAt       = last_changed_at;
    }
}
